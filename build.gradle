ext.masterVersion = '2.0.2-SNAPSHOT'
ext.encoding = 'UTF-8'

version = masterVersion

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.8.0'
    }
}

allprojects {
    apply plugin: 'groovy'

    task gitCommitDev(dependsOn: [clean, build]) << {
        def grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
        grgit.checkout(branch: 'master')
        grgit.add(patterns: ['.'], update: true)
        grgit.commit(message: "${version.replace('-SNAPSHOT', '')} dev start")
    }

    task gitCommitRelease(dependsOn: [clean, build]) << {
        def grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
        grgit.checkout(branch: "${version}-release", createBranch: true)
        grgit.add(patterns: ['.'], update: true)
        grgit.commit(message: "${version} released")
    }

    task gitMergeReleaseBranch << {
        def grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
        def releaseBranch = "${version}-release"
        grgit.checkout(branch: 'master')
        grgit.merge(head: releaseBranch, mode: org.ajoberstar.grgit.operation.MergeOp.Mode.CREATE_COMMIT)
    }

    task gitTag << {
        def grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
        grgit.tag.add(name: version, pointsTo: "${version}-release")
    }

    task gitPush << {
        def grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
        grgit.push()
        grgit.push(tags: true)
    }

    task gitReset << {
        def revision = project.hasProperty('revision') ? project.revision : 'FETCH_HEAD'
        def grgit = org.ajoberstar.grgit.Grgit.open(project.projectDir)
        grgit.reset(commit: revision,  mode: org.ajoberstar.grgit.operation.ResetOp.Mode.HARD)
    }

    build.mustRunAfter clean
}

subprojects {
    apply plugin: 'groovy'

    task replaceVersion << {
        ant.replaceregexp(
            match: /(version = ')[^']*(')/,
            replace: "\\1${masterVersion}\\2",
            encoding: encoding,
            flags: 'g') {
                fileset(dir: project.projectDir) {
                    include(name: 'build.gradle')
                }
            }
    }

    clean.mustRunAfter replaceVersion
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

